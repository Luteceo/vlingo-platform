import groovy.json.*

plugins {
  id 'org.ajoberstar.grgit' version '1.7.2'
}

ext.githubUri = 'https://github.com/vlingo/'
ext.checkoutDir = '../'

ext.repos = [
  'vlingo-common': [version: '0.3.3'],
  'vlingo-actors': [version: '0.4.5'],
  'vlingo-auth': [version: '0.3.4'],
  'vlingo-cluster': [version: '0.3.3'],
  'vlingo-directory': [version: '0.5.3'],
  'vlingo-http': [version: '0.3.3'],
  'vlingo-maven-plugin': [version: '0.2.2'],
  'vlingo-wire': [version: '0.3.4'],
]

// simple run gradle or ./gradlew
defaultTasks 'sync'

// first do this
task checkout {
  description 'checkout or update all vlingo repositories'

  doLast {
    repos.each { repo, _config ->
      cloneOrPull repo
    }
  }
}

// then that
task syncVersions {
  description 'synchronizes the repo and dependency versions'

  doLast {
    repos.each { repo, config ->
      replaceVersionIn repo, config
      syncPomVersionsIn repo, repos
    }
  }
}

// default umbrella task
task sync(dependsOn: [
  checkout,
  syncVersions,
]) {
  description 'perform all maintenance actions at once'
}

def cloneOrPull(repo) {
      def grgit

      def dir = "$checkoutDir$repo"
      def uri = "$githubUri$repo"

      if (!file(dir).exists()) {
        println "Cloning $uri into $dir"
        grgit = org.ajoberstar.grgit.Grgit.clone(dir: dir, uri: uri)
      } else {
        println "Pulling $uri into $dir"
        grgit = org.ajoberstar.grgit.Grgit.open(dir: dir)
        grgit.pull()
      }

      def lastCommit = new JsonBuilder(grgit.log(maxCommits: 1).last()).toPrettyString()
      println "Last commit: $lastCommit"

}

def replaceVersionIn(repo, config) {
  def dir = "$checkoutDir$repo"
  def replacementSets = [
    [name: 'README.md', versionRegex: /(<version>)(\S+)(<\/version>)/, replacement: "\\1${config.version}\\3"],
    [name: 'README.md', versionRegex: /(compile.*\:)(\S+)(\')/, replacement: "\\1${config.version}\\3"],
    [name: 'bintray.json', versionRegex: /(\s*\"version\":\s*\{\s*\"name\"\s*\:\s*\")(\S+)(\")/, replacement: "\\1${config.version}\\3"],
    [name: 'bintray.json', versionRegex: /(${repo}\/)(\S+)(\/)/, replacement: "\\1${config.version}\\3"],
    [name: 'bintray.json', versionRegex: /(${repo}-)(\S+)(\.)/, replacement: "\\1${config.version}\\3"],
  ]

  replacementSets.each { repl ->
    def path = "$dir/$repl.name"
    println "Syncing $path to $config.version"

    // simple regex replacements
    ant.replaceregexp(
      match: repl.versionRegex,
      replace: repl.replacement,
      encoding: 'UTF-8',
      flags: 'g') {
        fileset(dir: dir) {
            include(name: repl.name)
        }
    }
  }
}

def syncPomVersionsIn(repo, repos) {
  def pom = "$checkoutDir${repo}/pom.xml"
  println file(pom).exists()
}